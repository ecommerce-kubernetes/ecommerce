plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
    // api 명세 restdocs 플러그인
    id "org.asciidoctor.jvm.convert" version "3.3.2"
	id 'com.epages.restdocs-api-spec' version '0.19.2'
	id "org.sonarqube" version "7.1.0.6387"
	id 'jacoco'
}

group = 'com.example'
version = '1.2'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

sonar {
	properties {
		property "sonar.organization", "ecommerce-kubernetes"
		property "sonar.projectKey", "buynest-order-service"
		property "sonar.projectName", "Order Service"
		property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
    //아스키독
    asciidoctorExt
}

repositories {
    maven { url = uri("https://jitpack.io") }
	mavenCentral()
}

ext {
    //명세 스니펫
    snippetsDir = file('build/generated-snippets')
    set('springCloudVersion', "2024.0.1")
}

test {
    outputs.dir snippetsDir
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
}


asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
    sources {
        include("**/index.adoc")
    }
    baseDirFollowsSourceFile()

	doLast {
		copy {
			from outputDir
			into file("build/resources/main/static/docs")
		}
	}
}

bootJar {
	dependsOn asciidoctor
	from("${asciidoctor.outputDir}"){
		into 'static/docs'
	}
	from("build/api-spec") {
		into 'static/docs'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.cloud:spring-cloud-starter-bus-kafka'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'
	implementation 'org.springframework.boot:spring-boot-starter-security'

	//kafka module
	implementation 'com.github.ecommerce-kubernetes:common-dto:3.0.9'

	//QueryDsl
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'

	//Monitoring
	implementation 'io.micrometer:micrometer-tracing-bridge-brave'
	implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'com.github.loki4j:loki-logback-appender:2.0.3'

	//Test
	testImplementation 'org.projectlombok:lombok'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'com.h2database:h2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testAnnotationProcessor 'org.projectlombok:lombok'

	//API 명세
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.2'

	//flyway
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-mysql'
}


dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

clean {
	delete file('src/main/generated')
}

openapi3 {
	title = 'BuyNest Order API'
	description = '주문 서비스 API 명세서'
	version = '1.0.0'
	format = 'json'
	servers = [
			{
				url = 'https://api.buynest.cloud/order-service'
				description = '운영'
			},
	]
	outputFileNamePrefix = 'order-openapi'
}

jar {
	enabled = false
}

jacocoTestReport {
	reports { html.required = true }

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: "**/Q*")
		}))
	}
}

afterEvaluate {
	tasks.named('openapi3') {
		dependsOn tasks.named('test')
	}

	tasks.named('bootJar') {
		dependsOn tasks.named('openapi3')
	}
}