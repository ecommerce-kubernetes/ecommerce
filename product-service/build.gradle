plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
    // api 명세 restdocs 플러그인
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'com.epages.restdocs-api-spec' version '0.19.2'
    id 'jacoco'
}

group = 'com.example'
version = '1.2'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
    asciidoctorExt
}

repositories {
    maven { url = uri("https://jitpack.io") }
    mavenCentral()
}

ext {
    snippetsDir = file('build/generated-snippets')
	set('springCloudVersion', "2024.0.1")
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
    sources {
        include("**/index.adoc")
    }
    baseDirFollowsSourceFile()

    doLast {
        copy {
            from outputDir
            into file("build/resources/main/static/docs")
        }
    }
}

bootRun {
    dependsOn asciidoctor
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}"){
        into 'static/docs'
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-bus-kafka'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'

    //QueryDsl Implementation
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'

    //kafka dto
    implementation 'com.github.ecommerce-kubernetes:common-dto:3.0.9'

    //API 명세
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.2'

    //Test
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'com.h2database:h2'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

clean {
	delete file('src/main/generated')
}

openapi3 {
    title = 'BuyNest Product API'
    description = '상품 서비스 API 명세서'
    version = '1.0.0'
    format = 'json'
    servers = [
            {
                url = 'http://localhost:8000/product-service'
                description = '로컬'
            },
            {
                url = 'https://api.buynest.com'
                description = '운영'
            }
    ]
    outputFileNamePrefix = 'product-openapi'
}

// 빌드시 자동으로 api 파일이 gateway의 docs-client로 복사
tasks.register('copyOpenApiToDocusaurus', Copy) {
    dependsOn 'openapi3'
    //
    doFirst {
        delete file("../gateway-service/docs-client/docs/api/product")
        println "product 명세 폴더 삭제"
    }
    from layout.buildDirectory.dir("api-spec")
    into file("../gateway-service/docs-client/static/api-specs")
}

tasks.named('build') {
    dependsOn 'copyOpenApiToDocusaurus'
}

jacocoTestReport {
    reports { html.required = true }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: "**/Q*")
        }))
    }
}