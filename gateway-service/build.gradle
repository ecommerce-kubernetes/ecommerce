plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id "com.github.node-gradle.node" version "7.0.0"
}

group = 'com.example'
version = '1.5'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone'}
}

ext {
    set('springCloudVersion', "2024.0.1")
}

dependencies {
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

node {
    nodeProjectDir = file("${project.projectDir}/docs-client")
    version = '24.13.0'
    download = true
}

task npmInstallDocs(type: NpmTask) {
    args = ['install']
}

task genApiDocs(type: NpmTask) {
    args = ['run', 'docusaurus', '--', 'gen-api-docs', 'all']
    dependsOn npmInstallDocs
}

task buildDocs(type: NpmTask) {
    args = ['run', 'build']
    dependsOn genApiDocs
}

tasks.named('processResources') {
    // Docusaurus 빌드가 끝날 때까지 기다림
    dependsOn buildDocs

    // 빌드된 HTML 파일들을 가져와서 static 폴더에 넣음
    from("${project.projectDir}/docs-client/build") {
        into 'static'
    }
}