pipeline {
    agent any

    environment {
        GIT_REPO = 'https://github.com/ecommerce-kubernetes/ecommerce.git'
        BRANCH = 'develop'
        TARGET_DIR = 'config-service'
        SSH_CREDENTIALS_ID = 'ansible-key'
        REMOTE_HOST = '172.18.0.11'
        REMOTE_USER = 'root'
        REMOTE_PATH = '/ansible/playbooks/config-playbook'
    }

    triggers {
        // GitHub webhook이 작동하지 않을 때 Poll SCM 백업
        pollSCM('H/2 * * * *')
    }

    stages {
        stage('SCM Checkout (with Sparse Path)') {
            steps {
                echo '📥 config-service 디렉토리만 sparse checkout 중...'

                checkout([$class: 'GitSCM',
                    branches: [[name: "*/${BRANCH}"]],
                    userRemoteConfigs: [[
                        url: "${GIT_REPO}",
                        credentialsId: 'github_access_token'
                    ]],
                    extensions: [
                        [$class: 'SparseCheckoutPaths',
                            sparseCheckoutPaths: [[path: "${TARGET_DIR}"]]
                        ],
                        [$class: 'CleanCheckout']
                    ]
                ])
            }
        }

        stage('Check Changes') {
            steps {
                script {
                    if (!hasChangesIn("${TARGET_DIR}")) {
                        echo "❌ ${TARGET_DIR} 변경 없음. 스킵합니다."
                        currentBuild.result = 'SUCCESS'
                        error("No changes in ${TARGET_DIR}. Skipping pipeline.")
                    }
                }
            }
        }

        stage('Gradle Build') {
            steps {
                echo '⚙️ Gradle 빌드 시작'
                dir("${TARGET_DIR}") {
                    withCredentials([usernamePassword(credentialsId: 'github_credentials', usernameVariable: 'GITHUB_USERNAME', passwordVariable: 'GITHUB_TOKEN')]) {
                        sh '''
                            chmod +x gradlew
                            GITHUB_USERNAME=$GITHUB_USERNAME GITHUB_TOKEN=$GITHUB_TOKEN ./gradlew clean build
                        '''
                    }
                }
            }
        }

        stage('Copy to Ansible Server') {
            steps {
                echo '🚚 Ansible 서버로 JAR 파일 복사 중...'
                script {
                    def jarFiles = findFiles(glob: "${TARGET_DIR}/build/libs/*.jar")
                    def targetJar = jarFiles.find { file ->
                        !file.name.contains("plain") && file.name.endsWith(".jar")
                    }

                    if (targetJar) {
                        echo "📦 복사할 JAR 파일: ${targetJar.name}"
                        sshagent (credentials: ["${SSH_CREDENTIALS_ID}"]) {
                            // 1. 기존 JAR 파일 삭제
                            sh """
                                ssh -o StrictHostKeyChecking=no ${REMOTE_USER}@${REMOTE_HOST} \\
                                'rm -f ${REMOTE_PATH}/*.jar'
                            """

                            // 2. 새 JAR 파일을 이름을 바꿔서 복사 (discovery-service.jar)
                            sh """
                                scp -o StrictHostKeyChecking=no ${targetJar.path} ${REMOTE_USER}@${REMOTE_HOST}:${REMOTE_PATH}/config-service.jar
                            """
                        }
                    } else {
                        error("❌ plain이 아닌 JAR 파일을 찾을 수 없습니다.")
                    }
                }
            }
        }

        stage('Run Ansible Playbook') {
            steps {
                echo '🚀 Ansible 플레이북 실행 중...'
                sshagent (credentials: ["${SSH_CREDENTIALS_ID}"]) {
                    sh """
                        ssh -o StrictHostKeyChecking=no ${REMOTE_USER}@${REMOTE_HOST} \\
                        'ansible-playbook -i /ansible/inventory/hosts /ansible/playbooks/config-playbook/config-playbook.yml'
                    """
                }
            }
        }
    }

    post {
        success {
            echo '✅ config-service 배포 완료!'
        }
        failure {
            echo '❌ 빌드 또는 배포 실패!'
        }
    }
}

def hasChangesIn(String dir) {
    return sh(
        script: "git diff --name-only HEAD~1 HEAD | grep ^${dir}/ || true",
        returnStdout: true
    ).trim() != ""
}

